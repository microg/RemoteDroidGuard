/*
 * SPDX-FileCopyrightText: 2016, microG Project Team
 * SPDX-License-Identifier: Apache-2.0
 */

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

dependencies {
    implementation "org.microg:safe-parcel:$safeParcelVersion"
}

group = 'org.microg.gms'

android {
    compileSdkVersion androidCompileSdk
    buildToolsVersion "$androidBuildVersionTools"

    defaultConfig {
        versionName version
        minSdkVersion androidMinSdk
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                pom {
                    name = 'Remote DroidGuard Client'
                    description = 'Remote DroidGuard client library'
                    url = 'https://github.com/microg/RemoteDroidGuard'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'microg'
                            name = 'microG Team'
                        }
                        developer {
                            id = 'mar-v-in'
                            name = 'Marvin W.'
                        }
                    }
                    scm {
                        url = 'https://github.com/microg/RemoteDroidGuard'
                        connection = 'scm:git:https://github.com/microg/RemoteDroidGuard.git'
                        developerConnection = 'scm:git:ssh://github.com/microg/RemoteDroidGuard.git'
                    }
                }

                from components.release
            }
        }
        if (project.hasProperty('sonatype.username')) {
            repositories {
                maven {
                    name = 'sonatype'
                    url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                    credentials {
                        username project.getProperty('sonatype.username')
                        password project.getProperty('sonatype.password')
                    }
                }
            }
        }
    }
    if (project.hasProperty('signing.keyId')) {
        signing {
            sign publishing.publications
        }
    }
}
