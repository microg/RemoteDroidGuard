apply plugin: 'com.android.application'

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile 'com.squareup.wire:wire-runtime:1.6.1'
    compile ':droidguasso:@jar'
    compile project(':remote-droid-guard-lib')
}

String getMyVersionName() {
    def stdout = new ByteArrayOutputStream()
    if (rootProject.file("gradlew").exists())
        exec {
            commandLine 'git', 'describe', '--tags', '--always', '--dirty'; standardOutput = stdout
        }
    else // automatic build system, don't tag dirty
        exec { commandLine 'git', 'describe', '--tags', '--always'; standardOutput = stdout }
    return stdout.toString().trim().substring(1)
}

int getMyVersionCode() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', '--count', "HEAD"
        standardOutput = stdout
    }
    return Integer.parseInt(stdout.toString().trim())
}

android {
    compileSdkVersion androidCompileSdk()
    buildToolsVersion "$androidBuildVersionTools"

    defaultConfig {
        versionName getMyVersionName()
        versionCode getMyVersionCode()
        jackOptions {
            enabled true
        }
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/protos-java'
        }
    }

    dexOptions {
        additionalParameters "--core-library"
    }
}

if (file('user.gradle').exists()) {
    apply from: 'user.gradle'
}
